<?xml version="1.0" encoding="utf-8"?>
<!--

Licensed to the Apache Software Foundation (ASF) under one or more
contributor license agreements.  See the NOTICE file distributed with
this work for additional information regarding copyright ownership.
The ASF licenses this file to You under the Apache License, Version 2.0
(the "License"); you may not use this file except in compliance with
the License.  You may obtain a copy of the License at

http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.

-->
<s:ButtonBar width="80" arrowKeysWrapFocus="true" change="handleChange(event);" mouseFocusEnabled="false" xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:mx="library://ns.adobe.com/flex/mx"
			 xmlns:s="library://ns.adobe.com/flex/spark">
	<fx:Script>
		<![CDATA[
			import flashx.textLayout.formats.TextAlign;
			import flashx.textLayout.formats.TextLayoutFormat;
			import mx.collections.ArrayList;
			import mx.events.FlexEvent;
			import spark.components.TextArea;
			import spark.events.TextOperationEvent;

			[Embed(source = "../../../../assets/tools/center.png")]
			private const CENTER:Class;
			[Embed(source = "../../../../assets/tools/justify.png")]
			private const JUSTIFY:Class;
			[Embed(source = "../../../../assets/tools/left.png")]
			private const LEFT:Class;
			[Embed(source = "../../../../assets/tools/right.png")]
			private const RIGHT:Class;

			private var _textArea:TextArea;

			/**
			 *  The textArea that this component interacts with
			 */
			public function get textArea():TextArea
			{
				return _textArea;
			}

			/**
			 *  @private
			 */
			public function set textArea(value:TextArea):void
			{
				if (_textArea)
				{
					_textArea.removeEventListener(FlexEvent.SELECTION_CHANGE, handleSelectionChange);
				}
				_textArea = value;
				if (_textArea)
				{
					_textArea.addEventListener(FlexEvent.SELECTION_CHANGE, handleSelectionChange, false, 0, true);
					handleSelectionChange();
				}
			}

			/**
			 *  @private
			 */
			protected override function createChildren():void
			{
				super.createChildren();

				var dp:ArrayList = new ArrayList();
				dp.addItem({icon: LEFT, toolTip: "Left align text", value: TextAlign.LEFT});
				dp.addItem({icon: CENTER, toolTip: "Left align text", value: TextAlign.CENTER});
				dp.addItem({icon: RIGHT, toolTip: "Left align text", value: TextAlign.RIGHT});
				dp.addItem({icon: JUSTIFY, toolTip: "Left align text", value: TextAlign.JUSTIFY});
				this.dataProvider = dp;
			}

			/**
			 *  @private
			 */
			private function handleChange(e:Event):void
			{
				if (this.selectedItem)
				{
					var txtLayFmt:TextLayoutFormat = _textArea.getFormatOfRange(null, _textArea.selectionAnchorPosition, _textArea.selectionActivePosition);
					txtLayFmt.textAlign = this.selectedItem.value;
					_textArea.setFormatOfRange(txtLayFmt, _textArea.selectionAnchorPosition, _textArea.selectionActivePosition);
					_textArea.setFocus();
					_textArea.dispatchEvent(new TextOperationEvent(TextOperationEvent.CHANGE));
				}
			}

			/**
			 *  @private
			 */
			private function handleSelectionChange(e:FlexEvent = null):void
			{
				var format:TextLayoutFormat = _textArea.getFormatOfRange(null, _textArea.selectionAnchorPosition, _textArea.selectionActivePosition);

				switch (format.textAlign)
				{
					case TextAlign.LEFT:
						this.selectedIndex = 0;
						break;
					case TextAlign.CENTER:
						this.selectedIndex = 1;
						break;
					case TextAlign.RIGHT:
						this.selectedIndex = 2;
						break;
					case TextAlign.JUSTIFY:
						this.selectedIndex = 3;
						break;
					default:
						this.selectedIndex = -1;
						break;
				}
			}
		]]>
	</fx:Script>
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
</s:ButtonBar>
